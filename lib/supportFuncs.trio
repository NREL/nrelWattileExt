name:predAnalyticsReadHis
doc:
  Predictive analytics helper function; reads history for **recs** and converts into a history grid compatible with a prediction call to Python via 'taskPredAnalyticsDocker()'
  
  **TO DO:** Refine documentation. Should this be 'nodoc'?
func
src:
  // Helper function to read history and prepare it for conversion to Python dataframe
  (recs, span) => do
    // Convert to record list
    recs = recs.toRecList
    
    // Read histories and define column names (via 'dis' tags)
    data: recs.map() p => do
      // Get column 'dis'
      // This defines CSV export column names, but doesn't affect Python data frame column names
      if (p.has("dis")) do
        // Explicit pre-defined 'dis' (if available)
        dis: p["dis"]
      
      else do
        // Extract 'dis' from point metadata in database
        dis: p.toRecId.readById.dis
      end
      
      // TO DO: Explicit column renaming required? Column name mapping performed on Python side?
      
      // Read history
      history: p.toRecId.hisRead(span) // toRecId() here prevents some hisRead() misfires
      
      // Check for empty history
      if (history.isEmpty) throw "No history found for input \"" + dis + "\" and timespan " + format(span)
      
      // Fix column 'dis' tag, and return
      history.addColMeta("v0", {dis:dis, -disMacro})
    end
    
    // Combine data, clip to window, and return
    hisJoin(data).hisClip
  end